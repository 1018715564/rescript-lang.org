---
title: "Promise"
description: "JS Promise handling in ReScript"
canonical: "/docs/manual/latest/promise"
---

# Async & Promise

ReScript's primary mechanism for async programming is the same as JavaScript's (callbacks and promises), since we compile cleanly to JavaScript and would like to avoid dragging in a heavy custom runtime.

However, it is planned for us to introduce a coroutine-like feature in the future; for that reason, we're postponing introducing the keywords `async` and `await` into the language; though our (upcoming) Promise API bindings revamp + [pipe](pipe) will make your async code already look better than otherwise.

## Promise

ReScript has built-in support for [JavaScript promises](api/js/promise). The 3 functions you generally need are:

- `Js.Promise.resolve: 'a => Js.Promise.t('a)`
- `Js.Promise.then_: ('a => Js.Promise.t('b), Js.Promise.t('a)) => Js.Promise.t('b)`
- `Js.Promise.catch: (Js.Promise.error => Js.Promise.t('a), Js.Promise.t('a)) => Js.Promise.t('a)`

Additionally, here's the type signature for creating a promise on the ReScript side:

```res
Js.Promise.make: (
  (
    ~resolve: (. 'a) => unit,
    ~reject: (. exn) => unit
  ) => unit
) => Js.Promise.t<'a>
```

This type signature means that `make` takes a callback that takes 2 named arguments, `resolve` and `reject`. Both arguments are themselves [uncurried callbacks](
function.md#uncurried-function) (with a dot). `make` returns the created promise.

## Usage

Using the [pipe operator](pipe.md):

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let myPromise = Js.Promise.make((~resolve, ~reject) => resolve(. 2))

myPromise
-> Js.Promise.then_(value => {
     Js.log(value)
     Js.Promise.resolve(value + 2)
   }, _)
-> Js.Promise.then_(value => {
     Js.log(value)
     Js.Promise.resolve(value + 3)
   }, _)
-> Js.Promise.catch(err => {
     Js.log2("Failure!!", err)
     Js.Promise.resolve(-2)
   }, _)
```
```js
var myPromise = new Promise(function (resolve, reject) {
  return resolve(2);
});

myPromise
  .then(function (value) {
    console.log(value);
    return Promise.resolve((value + 2) | 0);
  })
  .then(function (value) {
    console.log(value);
    return Promise.resolve((value + 3) | 0);
  })
  .catch(function (err) {
    console.log("Failure!!", err);
    return Promise.resolve(-2);
  });
```

</CodeTab>

## Tips & Tricks

Promises aren't the best abstractions, especially in JavaScript. When used inside React or ReasonReact, [it is almost **always** wrong](https://twitter.com/search?q=(async%20OR%20promise)%20(from%3Asebmarkbage)&src=typed_query&f=live).

For that reason, we actually recommend judicious usages of callback-based apis, [like so](https://gist.github.com/chenglou/b6cf738a5d7adbde2ee008eb93117b49).

This applies to other leaky, slow and uncancellable, and [colored](https://journal.stuffwithstuff.com/2015/02/01/what-color-is-your-function/) promise abstractions like `fetch`. Prefer using them only for interop, not for your core logic.
