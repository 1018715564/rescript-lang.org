# Interop Cheatsheet

<!-- TODO: this should be a glossary instead -->

## Raw JS

```res
let add = %raw("(a + b) => a + b")
%%raw("const a = 1")
```

## Global Value

```res
@bs.val external setTimeout: (unit => unit, int) => float = "setTimeout"
```

## Global Module's Value

```res
@bs.val @bs.scope("Math")
external random: unit => float = "random"

let someNumber = random()

@bs.val @bs.scope(("window", "location", "ancestorOrigins"))
external length: int = "length"
```

## Nullable

```res
let a = Some(5) // compiles to 5
let b = None // compiles to undefined
```

Handling a value that can be `undefined` and `null`, by ditching the `option` type and using `Js.Nullable.t`:

```res
let jsNull = Js.Nullable.null;
let jsUndefined = Js.Nullable.undefined;

let result1: Js.Nullable.t(string) = Js.Nullable.return("hello");
let result2: Js.Nullable.t(int) = Js.Nullable.fromOption(Some(10));
let result3: option(int) = Js.Nullable.toOption(Js.Nullable.return(10));
```

## JS Object

- [Bind to a JS object as a ReScript record](bind-to-js-object#bind-to-record-like-js-objects).
- [Bind to a JS object that acts like a hash map](bind-to-js-object#bind-to-hash-map-like-js-object).
- [Bind to a JS object that's a class](bind-to-js-object#bind-to-a-js-object-thats-a-class).

## Function

### Object Method & Chaining

```res
@bs.send external map: (array<'a>, 'a => 'b) => array<'b> = "map"
@bs.send external filter: (array<'a>, 'a => 'b) => array<'b> = "filter"

[1, 2, 3]
  ->map(a => a + 1)
  ->filter(a => a mod 2 == 0)
  ->Js.log
```

### Variadic Arguments

```res
@bs.module("path") @bs.variadic
external join: array<string> => string = "join"
```

### Polymorphic Function

```res
@bs.module("Drawing") external drawCat: unit => unit = "draw"
@bs.module("Drawing") external drawDog: (~giveName: string) => unit = "draw"
```

```res
@bs.val
external padLeft: (
  string,
  @bs.unwrap [
    | #Str(string)
    | #Int(int)
  ])
  => string = "padLeft"

padLeft("Hello World", #Int(4))
padLeft("Hello World", #Str("Message from ReScript: "))
```

## JS Module Interop

[See here](import-from-export-to-js.md)

## Dangerous Type Cast

Final escape hatch converter. Do not abuse.

```res
external convertToFloat : int => float = "%identity"
let age = 10
let gpa = 2.1 +. convertToFloat(age)
```

## List of `@bs` Decorators

- `@bs.val`
- `@bs.scope`
- `@bs.get`
- `@bs.set`
- `@bs.send`
- `@bs.string`
- `@bs.int`
- `@bs.unwrap`
- `@bs.new`
- `@bs.as`
- `@bs.variadic`
- `@bs.inline`
- `@bs.meth`
