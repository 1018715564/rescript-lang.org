---
title: "Interop Cheatsheet"
description: "Cheatsheet for various interop scenarios in ReScript"
canonical: "/docs/manual/latest/interop-cheatsheet"
---

# Interop Cheatsheet

This is a glossary with examples. All the features are described by later pages.

## List of Decorators

<!-- Synced from https://github.com/rescript-lang/syntax/blob/123760c5a264da5288eeee5213ddd25eb86d62fe/src/res_printer.ml#L19-L51 -->

### Attributes

- `@bs.as`: [here](attribute#usage), [here](bind-to-js-function#fixed-arguments), [here](bind-to-js-function#constrain-arguments-better) and [here](generate-converters-accessors#usage-3)
- [`@bs.deriving`](generate-converters-accessors#generate-functions--plain-values-for-variants)
- [`@bs.get`](bind-to-js-object#bind-using-special-bs-getters--setters)
- [`@bs.get_index`](bind-to-js-object#bind-using-special-bs-getters--setters)
<!-- - `@bs.ignore` -->
- [`@bs.inline`](inlining-constants)
- [`@bs.int`](bind-to-js-function#constrain-arguments-better)
<!-- - `@bs.meth` -->
- [`@bs.module`](import-from-export-to-js#import-a-javascript-modules-content)
- [`@bs.new`](bind-to-js-object#bind-to-a-js-object-thats-a-class)
- [`@bs.obj`](generate-converters-accessors#convert-external-into-js-object-creation-function)
- [`@bs.optional`](generate-converters-accessors#optional-labels)
- [`@bs.return`](bind-to-js-function#function-nullable-return-value-wrapping)
- `@bs.send`: [here](bind-to-js-function#object-method) and [here](pipe#js-method-chaining)
- [`@bs.scope`](bind-to-global-js-values#global-modules)
- [`@bs.set`](bind-to-js-object#bind-using-special-bs-getters--setters)
- [`@bs.set_index`](bind-to-js-object#bind-using-special-bs-getters--setters)
- [`@bs.variadic`](bind-to-js-function#variadic-function-arguments)
- [`@bs.string`](bind-to-js-function#constrain-arguments-better)
- [`@bs.this`](bind-to-js-function#modeling-this-based-callbacks)
- [`@bs.uncurry`](bind-to-js-function#extra-solution)
- [`@bs.unwrap`](bind-to-js-function#trick-2-polymorphic-variant--bsunwrap)
- [`@bs.val`](bind-to-global-js-values#global-modules)

- [`@deprecated`](attribute#usage)
- [`genType`](https://github.com/reason-association/genType)
- [`@JSX`](jsx)
- `@react.component`: [here](/docs/react/latest/introduction) and [here](https://github.com/reasonml/reason-react)
- [`@warning`](attribute#usage)
- [`@unboxed`](unboxed)

### Extension Points

- [`%debugger`](embed-raw-javascript#debugger)
- [`%external`](bind-to-global-js-values#special-global-values)
<!-- - `%node` -->
<!-- - `%obj` -->
- [`%raw`](embed-raw-javascript#paste-raw-js-code)
- [`%re`](primitive-types#regular-expression)

## Raw JS

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let add = %raw("(a, b) => a + b")
%%raw("const a = 1")
```
```js
var add = ((a, b) => a + b);
const a = 1
```

</CodeTab>

## Global Value

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@bs.val external setTimeout: (unit => unit, int) => float = "setTimeout"
```
```js
// Empty output
```

</CodeTab>

## Global Module's Value

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@bs.val @bs.scope("Math")
external random: unit => float = "random"

let someNumber = random()

@bs.val @bs.scope(("window", "location", "ancestorOrigins"))
external length: int = "length"
```
```js
var someNumber = Math.random();
```

</CodeTab>

## Nullable

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let a = Some(5) // compiles to 5
let b = None // compiles to undefined
```
```js
var a = 5;
var b;
```

</CodeTab>

Handling a value that can be `undefined` and `null`, by ditching the `option` type and using `Js.Nullable.t`:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let jsNull = Js.Nullable.null
let jsUndefined = Js.Nullable.undefined
let result1: Js.Nullable.t<string> = Js.Nullable.return("hello")
let result2: Js.Nullable.t<int> = Js.Nullable.fromOption(Some(10))
let result3: option<int> = Js.Nullable.toOption(Js.Nullable.return(10))
```
```js
var Caml_option = require("./stdlib/caml_option.js");
var Js_null_undefined = require("./stdlib/js_null_undefined.js");

var jsNull = null;
var jsUndefined;
var result1 = "hello";
var result2 = Js_null_undefined.fromOption(10);
var result3 = Caml_option.nullable_to_opt(10);
```

</CodeTab>

## JS Object

- [Bind to a JS object as a ReScript record](bind-to-js-object#bind-to-record-like-js-objects).
- [Bind to a JS object that acts like a hash map](bind-to-js-object#bind-to-hash-map-like-js-object).
- [Bind to a JS object that's a class](bind-to-js-object#bind-to-a-js-object-thats-a-class).

## Function

### Object Method & Chaining

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@bs.send external map: (array<'a>, 'a => 'b) => array<'b> = "map"
@bs.send external filter: (array<'a>, 'a => 'b) => array<'b> = "filter"
[1, 2, 3]
  ->map(a => a + 1)
  ->filter(a => mod(a, 2) == 0)
  ->Js.log
```
```js
console.log(
  [1, 2, 3]
    .map(function (a) {
      return (a + 1) | 0;
    })
    .filter(function (a) {
      return a % 2 === 0;
    })
);
```

</CodeTab>

### Variadic Arguments

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@bs.module("path") @bs.variadic
external join: array<string> => string = "join"
```
```js
// Empty output
```

</CodeTab>

### Polymorphic Function

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@bs.module("Drawing") external drawCat: unit => unit = "draw"
@bs.module("Drawing") external drawDog: (~giveName: string) => unit = "draw"
```
```js
// Empty output
```

</CodeTab>

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@bs.val
external padLeft: (
  string,
  @bs.unwrap [
    | #Str(string)
    | #Int(int)
  ])
  => string = "padLeft"

padLeft("Hello World", #Int(4))
padLeft("Hello World", #Str("Message from ReScript: "))
```
```js
padLeft("Hello World", 4);
padLeft("Hello World", "Message from ReScript: ");
```

</CodeTab>

## JS Module Interop

[See here](import-from-export-to-js.md)

## Dangerous Type Cast

Final escape hatch converter. Do not abuse.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
external convertToFloat: int => float = "%identity"
let age = 10
let gpa = 2.1 +. convertToFloat(age)
```
```js
var age = 10;
var gpa = 2.1 + 10;
```

</CodeTab>
