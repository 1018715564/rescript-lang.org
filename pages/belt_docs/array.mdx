import { make as Layout } from "../../layouts/BeltDocsLayout.bs.js";
export default Layout;

# Array

Utililites for Array functions.

## length

```re
let length: array('a) => int;
```

return the size of the array

## size

```re
let size: array('a) => int;
```

See Belt_Array.length

## get

```re
let get: (array('a), int) => option('a);
```

If `i <= 0 <= length(arr)` returns `Some(value)` where `value` is the item at index `i`.
If `i` is out of range returns `None`

## getExn

```re
let getExn: (array('a), int) => 'a;
```

Raise an exception if `i` is out of range.
Otherwise return the value at index `i` in `arr`.

## getUnsafe

```re
let getUnsafe: (array('a), int) => 'a;
```

Unsafe

no bounds checking; this would cause type error if `i` does not stay within range

## getUndefined

```re
let getUndefined: (array('a), int) => Js.undefined('a);
```

It does the samething in the runtime as `Belt_Array.getUnsafe` it is type safe since the return type still track whether it is in range or not

## set

```re
let set: (array('a), int, 'a) => bool;
```

`set(arr, n, x);` modifies `arr` in place; it replaces the nth element of `arr` with `x`.

Returns false means not updated due to out of range.

## setExn

```re
let setExn: (array('a), int, 'a) => unit;
```

`setExn(arr, i, x);` raise an exception if `i` is out of range.

## setUnsafe

```re
let setUnsafe: (array('a), int, 'a) => unit;
```

## shuffleInPlace

```re
let shuffleInPlace: array('a) => unit;
```

`shuffleInPlace(arr)` randomly re-orders the items in `arr`

## shuffle

```re
let shuffle: array('a) => array('a);
```

Returns a fresh array with items in original array randomly shuffled.

## reverseInPlace

```re
let reverseInPlace: array('a) => unit;
```

`reverseInPlace(arr)` reverses items in `arr` in place.

Examples

```re
let arr = [|10, 11, 12, 13, 14|];

let () = reverseInPlace(arr);

arr == [|14, 13, 12, 11, 10|];
```

## reverse

```re
let reverse: array('a) => array('a);
```

`reverse(arr)` returns a fresh array with items in arr in reverse order.

Examples

```re
reverse([|10, 11, 12, 13, 14|]) == [|14, 13, 12, 11, 10|];
```

## makeUninitialized

```re
let makeUninitialized: int => array(Js.undefined('a));
```

`makeUninitialized(n)` creates an array of length `n` filled with the undefined value. You must specify the type of data that will eventually fill the array.

Examples

```re
let arr: array(Js.undefined(string)) = makeUninitialized(5);

getExn(arr, 0) == Js.undefined;
```

## makeUninitializedUnsafe

```re
let makeUninitializedUnsafe: int => array('a);
```

Unsafe

Examples

```re
let arr = Belt.Array.makeUninitializedUnsafe(5);

let () = Js.log(Belt.Array.getExn(arr, 0)); /* undefined */

Belt.Array.setExn(arr, 0, "example");

let () = Js.log(Belt.Array.getExn(arr, 0) == "example");
```

## make

```re
let make: (int, 'a) => array('a);
```

`make(n, e)` return an array of size `n` filled with value `e`.
Returns an empty array when `n` is negative.

## range

```re
let range: (int, int) => array(int);
```

`range(start, finish);` create an inclusive array.

Examples

```re
range(0, 3) == [|0, 1, 2, 3|];

range(3, 0) == [||];

range(3, 3) == [|3|];
```

## rangeBy

```re
let rangeBy: (int, int, ~step: int) => array(int);
```

`rangeBy(start, finish, ~step);`

Returns empty array when step is 0 or negative. It also return an empty array when `start > finish`.

Examples

```re
rangeBy(0, 10, ~step=3) == [|0, 3, 6, 9|];

rangeBy(0, 12, ~step=3) == [|0, 3, 6, 9, 12|];

rangeBy(33, 0, ~step=1) == [||];

rangeBy(33, 0, ~step=-1) == [||];

rangeBy(3, 12, ~step=-1) == [||];

rangeBy(3, 3, ~step=0) == [||];

rangeBy(3, 3, ~step=1) == [|3|];
```

## makeByU

```re
let makeByU: (int, [@bs] (int => 'a)) => array('a);
```

## makeBy

```re
let makeBy: (int, int => 'a) => array('a);
```

`makeBy(n, f);`

Return an empty array when n is negative return an array of size n populated by `f(i)` start from `0` to `n - 1`.

Examples

```re
makeBy(5, (i) => i) == [|0, 1, 2, 3, 4|];

makeBy(5, (i) => i * i) == [|0, 1, 4, 9, 16|];
```

## makeByAndShuffleU

```re
let makeByAndShuffleU: (int, [@bs] (int => 'a)) => array('a);
```

## makeByAndShuffle

```re
let makeByAndShuffle: (int, int => 'a) => array('a);
```

`makeByAndShuffle(n, f);`

Equivalent to `shuffle(makeBy(n, f));`

## zip

```re
let zip: (array('a), array('b)) => array(('a, 'b));
```

`zip(a, b);`

Create an array of pairs from corresponding elements of a and b. Stop with the shorter array.

Examples:

```re
Belt.Array.zip([|1, 2|], [|3, 4, 5|]) == [|(1, 3), (2, 4)|]
```

## zipByU

```re
let zipByU: (array('a), array('b), [@bs] (('a, 'b) => 'c)) => array('c);
```

## zipBy

```re
let zipBy: (array('a), array('b), ('a, 'b) => 'c) => array('c);
```

`zipBy(xs, ys, f);`

Create an array by applying `f` to corresponding elements of `xs` and `ys`. Stops with shorter array.

Equivalent to `map(zip(xs, ys), ((a, b)) => f(a, b));`

Examples

```re
zipBy([|1, 2, 3|], [|4, 5|], (a, b) => 2 * a + b) == [|6, 9|];
```

## unzip

```re
let unzip: array(('a, 'b)) => (array('a), array('b));
```

`unzip(a);` takes an array of pairs and creates a pair of arrays. The first array contains all the first items of the pairs; the second array contains all the second items.

Examples

```re
unzip([|(1, 2), (3, 4)|]) == ([|1, 3|], [|2, 4|]);

unzip([|(1, 2), (3, 4), (5, 6), (7, 8)|]) == ([|1, 3, 5, 7|], [|2, 4, 6, 8|]);
```

## concat

```re
let concat: (array('a), array('a)) => array('a);
```

`concat(xs, ys);`

Returns a fresh array containing the concatenation of the arrays `v1` and `v2`;so even if `v1` or `v2` is empty; it can not be shared

Examples

```re
concat([|1, 2, 3|], [|4, 5|]) == [|1, 2, 3, 4, 5|];

concat([||], [|"a", "b", "c"|]) == [|"a", "b", "c"|];
```

## concatMany

```re
let concatMany: array(array('a)) => array('a);
```

`concatMany(xss);`

Returns a fresh array as the concatenation of `xss` (an array of arrays)

Examples

```re
concatMany([|[|1, 2, 3|], [|4, 5, 6|], [|7, 8|]|]) == [|1, 2, 3, 4, 5, 6, 7, 8|];
```

## slice

```re
let slice: (array('a), ~offset: int, ~len: int) => array('a);
```

`slice(xs, offset, len);` creates a new array with the len elements of `xs` starting at `offset` for

`offset` can be negative;and is evaluated as `length(xs) - offset(slice, xs) - 1(1);` means get the last element as a singleton array

`slice(xs, ~-len, len);` will return a copy of the array

if the array does not have enough data; `slice` extracts through the end of sequence.

if `len` is negative; returns the empty array.

Examples

```re
slice([|10, 11, 12, 13, 14, 15, 16|], ~offset=2, ~len=3) == [|12, 13, 14|];

slice([|10, 11, 12, 13, 14, 15, 16|], ~offset=-4, ~len=3) == [|13, 14, 15|];

slice([|10, 11, 12, 13, 14, 15, 16|], ~offset=4, ~len=9) == [|14, 15, 16|];
```

## sliceToEnd

```re
let sliceToEnd: (array('a), int) => array('a);
```

`sliceToEnd(xs, offset);` creates a new array with the elements of `xs` starting at `offset`

`offset` can be negative; and is evaluated as `length(xs) - offset(sliceToEnd, xs) - 1;` means get the last element as a singleton array

`sliceToEnd(xs, 0);` will return a copy of the array

Examples

```re
sliceToEnd([|10, 11, 12, 13, 14, 15, 16|], 2) == [|12, 13, 14, 15, 16|];

sliceToEnd([|10, 11, 12, 13, 14, 15, 16|], -4) == [|13, 14, 15, 16|];
```

## copy

```re
let copy: array('a) => array('a);
```

`copy(a);`

Returns a copy of a; that is; a fresh array containing the same elements as a.

## fill

```re
let fill: (array('a), ~offset: int, ~len: int, 'a) => unit;
```

`fill(arr, ~offset, ~len, x);`

Modifies `arr` in place, storing `x` in elements number `offset` to `offset + len - 1`.
`offset` can be negative; and is evaluated as `length(arr - offset);`

`fill(arr, ~offset=-1, ~len=1);` means fill the last element, if the array does not have enough data; `fill` will ignore it

Examples

```re
let arr = makeBy(5, (i) => i);

fill(arr, ~offset=2, ~len=2, 9);

arr == [|0, 1, 9, 9, 4|];

fill(arr, ~offset=7, ~len=2, 8);

arr == [|0, 1, 9, 9, 4|];
```

## blit

```re
let blit: (~src: array('a), ~srcOffset: int, ~dst: array('a), ~dstOffset: int, ~len: int) => unit;
```

`blit(~src=v1, ~srcOffset=o1, ~dst=v2, ~dstOffset=o2, ~len);`

copies `len` elements from array `v1`;starting at element number `o1`;to array `v2`, starting at element number `o2`.

It works correctly even if `v1` and `v2` are the same array;and the source and destination chunks overlap.

`offset` can be negative; `-1` means `len - 1`; if `len + offset` is still negative;it will be set as 0

For each of the examples;presume that `v1 == [|10, 11, 12, 13, 14, 15, 16, 17|];` and `v2 == [|20, 21, 22, 23, 24, 25, 26, 27|];`. The result shown is the content of the destination array.

Examples

```re
Belt.Array.blit(~src=v1, ~srcOffset=4, ~dst=v2, ~dstOffset=2, ~len=3)
|. [|20, 21, 14, 15, 16, 25, 26, 27|];

Belt.Array.blit(~src=v1, ~srcOffset=4, ~dst=v1, ~dstOffset=2, ~len=3)
|. [|10, 11, 14, 15, 16, 15, 16, 17|];
```

## blitUnsafe

```re
let blitUnsafe: (~src: array('a), ~srcOffset: int, ~dst: array('a), ~dstOffset: int, ~len: int) => unit;
```

Unsafe blit without bounds checking.

## forEachU

```re
let forEachU: (array('a), [@bs] ('a => unit)) => unit;
```

## forEach

```re
let forEach: (array('a), 'a => unit) => unit;
```

`forEach(xs, f);`

Call `f` on each element of `xs` from the beginning to end. `f` returns `unit`;so no new array is created. Use `forEach` when you are primarily concerned with repetitively creating side effects.

Examples

```re
forEach([|"a", "b", "c"|], (x) => Js.log("Item: " ++ x));

/*  prints:
      Item: a
      Item: b
      Item: c
    */
let total = ref(0);

forEach([|1, 2, 3, 4|], (x) => total := total^ + x);

total^ == 1 + 2 + 3 + 4;
```

## mapU

```re
let mapU: (array('a), [@bs] ('a => 'b)) => array('b);
```

## map

```re
let map: (array('a), 'a => 'b) => array('b);
```

`map(xs, f);`

Returns a new array by calling `f` for each element of `xs` from the beginning to end.

Examples

```re
map([|1, 2|], (x) => x + 1) == [|3, 4|];
```

## getByU

```re
let getByU: (array('a), [@bs] ('a => bool)) => option('a);
```

## getBy

```re
let getBy: (array('a), 'a => bool) => option('a);
```

`getBy(xs, p);`

Returns `Some(value)` for the first value in `xs` that satisifies the predicate function `p`; returns `None` if no element satisifies the function.

Examples

```re
getBy([|1, 4, 3, 2|], (x) => x mod 2 == 0) == Some(4);
getBy([|15, 13, 11|], (x) => x mod 2 == 0) == None;
```

## getIndexByU

```re
let getIndexByU: (array('a), [@bs] ('a => bool)) => option(int);
```

## getIndexBy

```re
let getIndexBy: (array('a), 'a => bool) => option(int);
```

`getIndexBy(xs, p);`

returns `Some(index)` for the first value in `xs` that satisifies the predicate function `p`;
returns `None` if no element satisifies the function.

Examples

```re
getIndexBy([|1, 4, 3, 2|], (x) => x mod 2 == 0) == Some(1);
getIndexBy([|15, 13, 11|], (x) => x mod 2 == 0) == None;
```

## keepU

```re
let keepU: (array('a), [@bs] ('a => bool)) => array('a);
```

## keep

```re
let keep: (array('a), 'a => bool) => array('a);
```

`keep(xs, p);`

Returns a new array that keep all elements satisfy `p`.

Examples

```re
keep([|1, 2, 3|], (x) => x mod 2 == 0) == [|2|];
```

## keepWithIndexU

```re
let keepWithIndexU: (array('a), [@bs] (('a, int) => bool)) => array('a);
```

## keepWithIndex

```re
let keepWithIndex: (array('a), ('a, int) => bool) => array('a);
```

`keepWithIndex(xs, p);`

Returns a new array that keep all elements satisfy `p`.

Examples

```re
keepWithIndex([|1, 2, 3|], (_x, i) => i == 1) == [|2|];
```

## keepMapU

```re
let keepMapU: (array('a), [@bs] ('a => option('b))) => array('b);
```

## keepMap

```re
let keepMap: (array('a), 'a => option('b)) => array('b);
```

`keepMap(xs, p);`

Returns a new array that keep all elements that return a non-None applied `p`.

Examples

```re
keepMap(
  [|1, 2, 3|],
  (x) =>
    if (x mod 2) {
      Some(x);
    } else {
      None;
    }
)
== [|2|];
```

## forEachWithIndexU

```re
let forEachWithIndexU: (array('a), [@bs] ((int, 'a) => unit)) => unit;
```

## forEachWithIndex

```re
let forEachWithIndex: (array('a), (int, 'a) => unit) => unit;
```

`forEachWithIndex(xs, f);`

The same as `Belt_Array.forEach`;
except that `f` is supplied two arguments: the index starting from 0 and the element from `xs`.

Examples

```re
forEach([|"a", "b", "c"|], (i, x) => Js.log("Item " ++ string_of_int(i) ++ " is " ++ x));

/*  prints:
      Item 0 is a
      Item 1 is b
      Item 2 is cc
    */
let total = ref(0);

forEachWithIndex([|10, 11, 12, 13|], (i, x) => total := total^ + x + i);

total^ == 0 + 10 + 1 + 11 + 2 + 12 + 3 + 13;
```

## mapWithIndexU

```re
let mapWithIndexU: (array('a), [@bs] ((int, 'a) => 'b)) => array('b);
```

## mapWithIndex

```re
let mapWithIndex: (array('a), (int, 'a) => 'b) => array('b);
```

`mapWithIndex(xs, f);`

`mapWithIndex(xs, f)` applies `f` to each element of `xs`. Function `f` takes two arguments: the index starting from 0 and the element from `xs`.

Examples

```re
mapWithIndex([|1, 2, 3|], (i, x) => i + x) == [|0 + 1, 1 + 2, 2 + 3|];
```

## partitionU

```re
let partitionU: (array('a), [@bs] ('a => bool)) => (array('a), array('a));
```

## partition

```re
let partition: (array('a), 'a => bool) => (array('a), array('a));
```

`partition(f, a)` split array into tuple of two arrays based on predicate `f`; first of tuple where predicate cause true, second where predicate cause false

Examples

```
partition([|1, 2, 3, 4, 5|], (x) => x mod 2 == 0) == ([|2, 4|], [|1, 2, 3|]);

partition([|1, 2, 3, 4, 5|], (x) => x mod 2 != 0) == ([|1, 2, 3|], [|2, 4|]);
```

## reduceU

```re
let reduceU: (array('b), 'a, [@bs] (('a, 'b) => 'a)) => 'a;
```

## reduce

```re
let reduce: (array('b), 'a, ('a, 'b) => 'a) => 'a;
```

`reduce(xs, init, f);`

Applies `f` to each element of `xs` from beginning to end. Function `f` has two parameters: the item from the list and an “accumulator”; which starts with a value of `init`. `reduce` returns the final value of the accumulator.

Examples

```re
reduce([|2, 3, 4|], 1, (+)) == 10;

reduce([|"a", "b", "c", "d"|], "", (++)) == "abcd";
```

## reduceReverseU

```re
let reduceReverseU: (array('b), 'a, [@bs] (('a, 'b) => 'a)) => 'a;
```

## reduceReverse

```re
let reduceReverse: (array('b), 'a, ('a, 'b) => 'a) => 'a;
```

`reduceReverse(xs, init, f);`

Works like `Belt_Array.reduce`; except that function `f` is applied to each item of `xs` from the last back to the first.

Examples

```re
reduceReverse([|"a", "b", "c", "d"|], "", (++)) == "dcba";
```

## reduceReverse2U

```re
let reduceReverse2U: (array('a), array('b), 'c, [@bs] (('c, 'a, 'b) => 'c)) => 'c;
```

## reduceReverse2

```re
let reduceReverse2: (array('a), array('b), 'c, ('c, 'a, 'b) => 'c) => 'c;
```

`reduceReverse2(xs, ys, init, f);`

Reduces two arrays xs and ys;taking items starting at `min(length(xs), length(ys))` down to and including zero.

Examples

```re
reduceReverse2([|1, 2, 3|], [|1, 2|], 0, (acc, x, y) => acc + x + y) == 6;
```

## reduceWithIndexU

```re
let reduceWithIndexU: (array('a), 'b, [@bs] (('b, 'a, int) => 'b)) => 'b;
```

## reduceWithIndex

```re
let reduceWithIndex: (array('a), 'b, ('b, 'a, int) => 'b) => 'b;
```

`reduceWithIndex(xs, f);`

Applies `f` to each element of `xs` from beginning to end. Function `f` has three parameters: the item from the array and an “accumulator”, which starts with a value of `init` and the index of each element. `reduceWithIndex` returns the final value of the accumulator.

Examples

```re
reduceWithIndex([|1, 2, 3, 4|], 0, (acc, x, i) => acc + x + i) == 16;
```

## someU

```re
let someU: (array('a), [@bs] ('a => bool)) => bool;
```

## some

```re
let some: (array('a), 'a => bool) => bool;
```

`some(xs, p);`

Returns true if at least one of the elements in `xs` satifies `p`; where `p` is a predicate: a function taking an element and returning a `bool`.

Examples

```re
some([|2, 3, 4|], (x) => x mod 2 == 1) == true;

some([|(-1), (-3), (-5)|], (x) => x > 0) == false;
```

## everyU

```re
let everyU: (array('a), [@bs] ('a => bool)) => bool;
```

## every

```re
let every: (array('a), 'a => bool) => bool;
```

`every(xs, p);`

Returns `true` if all elements satisfy `p`; where `p` is a predicate: a function taking an element and returning a `bool`.

Examples

```re
every([|1, 3, 5|], (x) => x mod 2 == 1) == true;

every([|1, (-3), 5|], (x) => x > 0) == false;
```

## every2U

```re
let every2U: (array('a), array('b), [@bs] (('a, 'b) => bool)) => bool;
```

## every2

```re
let every2: (array('a), array('b), ('a, 'b) => bool) => bool;
```

`every2(xs, ys, p);`

returns true if `p(xi, yi);` is true for all pairs of elements up to the shorter length (i.e. `min(length(xs), length(ys));`)

Examples

```re
every2([|1, 2, 3|], [|0, 1|], (>)) == true;

every2([||], [|1|], (x, y) => x > y) == true;

every2([|2, 3|], [|1|], (x, y) => x > y) == true;

every2([|0, 1|], [|5, 0|], (x, y) => x > y) == false;
```

## some2U

```re
let some2U: (array('a), array('b), [@bs] (('a, 'b) => bool)) => bool;
```

## some2

```re
let some2: (array('a), array('b), ('a, 'b) => bool) => bool;
```

`some2(xs, ys, p);`

returns true if `p(xi, yi);` is true for any pair of elements up to the shorter length (i.e. `min(length(xs), length(ys));`)

Examples

```re
some2([|0, 2|], [|1, 0, 3|], (>)) == true;

some2([||], [|1|], (x, y) => x > y) == false;

some2([|2, 3|], [|1, 4|], (x, y) => x > y) == true;
```

## cmpU

```re
let cmpU: (array('a), array('a), [@bs] (('a, 'a) => int)) => int;
```

## cmp

```re
let cmp: (array('a), array('a), ('a, 'a) => int) => int;
```

`cmp(xs, ys, f);`

Compared by length if `length(xs) != length(ys)`; returning -1 if `length(xs) < length(ys)` or 1 if `length(xs) > length(ys)`
Otherwise compare one by one `f(x, y);`. `f` returns
a negative number if `x` is “less than” `y`
zero if `x` is “equal to” `y`
a positive number if `x` is “greater than” `y`
The comparison returns the first non-zero result of `f`;or zero if `f` returns zero for all `x` and `y`.

Examples

```re
cmp([|1, 3, 5|], [|1, 4, 2|], (a, b) => compare(a, b)) == (-1);

cmp([|1, 3, 5|], [|1, 2, 3|], (a, b) => compare(a, b)) == 1;

cmp([|1, 3, 5|], [|1, 3, 5|], (a, b) => compare(a, b)) == 0;
```

## eqU

```re
let eqU: (array('a), array('a), [@bs] (('a, 'a) => bool)) => bool;
```

## eq

```re
let eq: (array('a), array('a), ('a, 'a) => bool) => bool;
```

`eq(xs, ys);`

return false if length is not the same
otherwise compare items one by one using `f(xi, yi);`; and return true if all results are true;false otherwise

Examples

```re
eq([|1, 2, 3|], [|(-1), (-2), (-3)|], (a, b) => abs(a) == abs(b)) == true;
```

## truncateToLengthUnsafe

```re
let truncateToLengthUnsafe: (array('a), int) => unit;
```

Unsafe `truncateToLengthUnsafe(xs, n);` sets length of array `xs` to `n`.

If `n` is greater than the length of `xs`; the extra elements are set to `Js.Null_undefined.null`.

If `n` is less than zero; raises a `RangeError`.

Examples

```re
let arr = [|"ant", "bee", "cat", "dog", "elk"|];

truncateToLengthUnsafe(arr, 3);

arr == [|"ant", "bee", "cat"|];
```
